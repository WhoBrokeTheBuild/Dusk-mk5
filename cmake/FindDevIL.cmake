# Copyright 2017 Stephen Lane-Walsh
# Licensed under the MIT License

# Find DevIL Include Files
#
# Output Variables
# - DEVIL_FOUND
# - IL_INCLUDE_DIRS
# - IL_LIBRARIES
# - ILU_LIBRARIES
# - ILUT_LIBRARIES
#

SET(
  DEVIL_SEARCH_PATHS
  ${DEVIL_PATH}
  $ENV{VS140COMNTOOLS}../../VC # VS 2015
  $ENV{VS120COMNTOOLS}../../VC # VS 2013
  $ENV{VS110COMNTOOLS}../../VC # VS 2012
  $ENV{VS100COMNTOOLS}../../VC # VS 2010
)

# Determine 32-bit or 64-bit library path suffixes
IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
	SET(LIB_PATH_SUFFIXES lib64 lib/x64 lib/amd64 lib)
ELSE()
	set(LIB_PATH_SUFFIXES lib/x86 lib)
ENDIF()

# Search DEVIL_PATH and special paths
FIND_PATH(
  IL_INCLUDE_DIRS
  NAMES IL/il.h
  PATHS ${DEVIL_SEARCH_PATHS}
  PATH_SUFFIXES include
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  IL_LIBRARIES
  NAMES IL
  PATHS ${DEVIL_SEARCH_PATHS}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  ILU_LIBRARIES
  NAMES ILU
  PATHS ${DEVIL_SEARCH_PATHS}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  ILUT_LIBRARIES
  NAMES ILUT
  PATHS ${DEVIL_SEARCH_PATHS}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)

# Search pkg-config
FIND_PACKAGE(PkgConfig)
PKG_CHECK_MODULES(PC_IL QUIET IL)
PKG_CHECK_MODULES(PC_ILU QUIET ILU)
PKG_CHECK_MODULES(PC_ILUT QUIET ILUT)

# Search CMake standard paths
FIND_PATH(
  IL_INCLUDE_DIRS
  NAMES IL/il.h
)
FIND_LIBRARY(
  IL_LIBRARIES
  NAMES IL
)
FIND_LIBRARY(
  ILU_LIBRARIES
  NAMES ILU
)
FIND_LIBRARY(
  ILUT_LIBRARIES
  NAMES ILUT
)

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(
  DEVIL
  REQUIRED_VARS
  IL_INCLUDE_DIRS
  IL_LIBRARIES
  ILU_LIBRARIES
)
