# Copyright 2017 Stephen Lane-Walsh
# Licensed under the MIT License

# Find DevIL Include Files
#
# Output Variables
# - DEVIL_FOUND
# - IL_INCLUDE_DIRS
# - IL_LIBRARIES
# - ILU_LIBRARIES
# - ILUT_LIBRARIES
#

# Determine 32-bit or 64-bit library path suffixes
IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
	SET(LIB_PATH_SUFFIXES lib64 lib)
ELSE()
	set(LIB_PATH_SUFFIXES lib)
ENDIF()

# Search DEVIL_PATH
FIND_PATH(
  IL_INCLUDE_DIRS
  NAMES IL/il.h
  PATHS ${DEVIL_PATH}
  PATH_SUFFIXES include
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  IL_LIBRARIES
  NAMES IL DevIL
  PATHS ${DEVIL_PATH}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  ILU_LIBRARIES
  NAMES ILU
  PATHS ${DEVIL_PATH}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  ILUT_LIBRARIES
  NAMES ILUT
  PATHS ${DEVIL_PATH}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)

# Search pkg-config
FIND_PACKAGE(PkgConfig QUIET)
PKG_CHECK_MODULES(IL QUIET IL)
PKG_CHECK_MODULES(ILU QUIET ILU)
PKG_CHECK_MODULES(ILUT QUIET ILUT)

# Search CMake standard paths
FIND_PATH(
  IL_INCLUDE_DIRS
  NAMES IL/il.h
)
FIND_LIBRARY(
  IL_LIBRARIES
  NAMES IL DevIL
)
FIND_LIBRARY(
  ILU_LIBRARIES
  NAMES ILU
)
FIND_LIBRARY(
  ILUT_LIBRARIES
  NAMES ILUT
)

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(
  DEVIL
  REQUIRED_VARS
  IL_INCLUDE_DIRS
  IL_LIBRARIES
  ILU_LIBRARIES
)
