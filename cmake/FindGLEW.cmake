# Copyright 2017 Stephen Lane-Walsh
# Licensed under the MIT License

# Find the OpenGL Extension Wrangler
#
# Output Variables
# - GLEW_FOUND
# - GLEW_INCLUDE_DIRS
# - GLEW_LIBRARIES
# - GLEW_STATIC_LIBRARIES
#

# Determine 32-bit or 64-bit library path suffixes
IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
	SET(LIB_PATH_SUFFIXES lib64 lib/x64 lib/amd64 lib)
ELSE()
	set(LIB_PATH_SUFFIXES lib/x86 lib)
ENDIF()

# Search GLEW_PATH
FIND_PATH(
  GLEW_INCLUDE_DIRS
  NAMES GL/glew.h
  PATHS ${GLEW_PATH}
  PATH_SUFFIXES include
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  GLEW_LIBRARIES
  NAMES glew glew32 GLEW
  PATHS ${GLEW_PATH}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)
FIND_LIBRARY(
  GLEW_STATIC_LIBRARIES
  NAMES glew32s
  PATHS ${GLEW_PATH}
  PATH_SUFFIXES ${LIB_PATH_SUFFIXES}
  NO_DEFAULT_PATH
)

# Search pkg-config
FIND_PACKAGE(PkgConfig)
PKG_CHECK_MODULES(GLEW QUIET GLEW)

# Search CMake standard paths
FIND_PATH(
  GLEW_INCLUDE_DIRS
  NAMES GL/glew.h
)
FIND_LIBRARY(
  GLEW_LIBRARIES
  NAMES glew glew32 GLEW
)
FIND_LIBRARY(
  GLEW_STATIC_LIBRARIES
  NAMES glew32s
)

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(
  GLEW
  REQUIRED_VARS
  GLEW_INCLUDE_DIRS
  GLEW_LIBRARIES
)
